plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jsonschema2pojo' version '1.1.3'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

configurations {
    openapiGenerator
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.3'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    openapiGenerator 'org.openapitools:openapi-generator-cli:7.4.0'
}

task generateApi(type: JavaExec) {
    group = 'openapi'
    description = 'Generuje k√≥d z OpenAPI specifikace'
    classpath = configurations.openapiGenerator
    mainClass.set('org.openapitools.codegen.OpenAPIGenerator')

    args = [
            'generate',
            '-g', 'spring',
            '-i', 'src/main/resources/openapi/api.yaml',
            '-o', "$buildDir/generated",
            '--additional-properties=useSpringBoot=true,interfaceOnly=true,modelPackage=com.example.api.model,apiPackage=com.example.api.controller,basePackage=com.example.api,useJakartaEe=true'
    ]
}

jsonSchema2Pojo {
    source = files("src/main/resources/schema")
    targetPackage = "com.example.mongo.model"
    targetDirectory = file("$buildDir/generated/src/main/java")
    includeGeneratedAnnotation = true
    useJakartaValidation = true
}

sourceSets {
    main {
        java {
            srcDir 'build/generated/src/main/java'
        }
    }
}